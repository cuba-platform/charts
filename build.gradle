/*
 * Copyright (c) 2011 Haulmont Technology Ltd. All Rights Reserved.
 * Haulmont Technology proprietary and confidential.
 * Use is subject to license terms.
 */

allprojects {
    artifactGroup = 'com.haulmont.charts'
    artifactVersion = '3.0'
    isSnapshot = true
}

buildscript {
    org.apache.ivy.util.url.CredentialsStore.INSTANCE.addCredentials(
            'Sonatype Nexus Repository Manager',
            'repository.haulmont.com',
            System.getenv('HAULMONT_REPOSITORY_USER'),
            System.getenv('HAULMONT_REPOSITORY_PASSWORD')
    )
    repositories {
        mavenLocal()
        mavenRepo(urls: "http://repository.haulmont.com:8587/nexus/content/groups/work")
    }
    dependencies {
        classpath group: 'com.haulmont.gradle', name: 'cuba-plugin', version: '1.0-SNAPSHOT'
    }
}

apply(plugin: 'idea')
apply(plugin: 'cuba')

def globalModule = project(':charts-global')
def webModule = project(':charts-web')

def platformVersion = '3.0-SNAPSHOT'

configure([globalModule, webModule]) {
    apply(plugin: 'java')
    apply(plugin: 'idea')
    apply(plugin: 'maven')
    apply(plugin: 'cuba')

    dependencies {
        compile(group: 'com.haulmont.bali', name: 'bali', version: platformVersion)
        compile(group: 'com.haulmont.chile', name: 'chile-core', version: platformVersion)
        compile(group: 'com.haulmont.chile', name: 'chile-jpa', version: platformVersion)
        compile(group: 'com.haulmont.cuba', name: 'cuba-global', version: platformVersion)
        testCompile(group: 'junit', name: 'junit', version: '4.5')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure(globalModule) {
    task enhance(dependsOn: [compileJava, processResources], type: CubaEnhancing) {
        persistenceXml = "$globalModule.projectDir/src/charts-persistence.xml"
        metadataXml = "$globalModule.projectDir/src/charts-metadata.xml"
    }
    classes.dependsOn enhance
}

configure(webModule) {
    configurations {
        webcontent
    }

    dependencies {
        compile(globalModule)
        compile(guiModule)
        compile(group: 'com.haulmont.cuba', name: 'cuba-web', version: platformVersion)
        webcontent(group: 'com.haulmont.cuba', name: 'cuba-web', version: platformVersion, classifier: 'web', ext: 'zip')
    }

    task deploy(dependsOn: assemble, type: CubaDeployment) {
        appName = 'app'
        appJars('bali', 'chile-core', 'chile-jpa',
                'cuba-global', 'cuba-client', 'cuba-gui', 'cuba-web',
                'charts-global', 'charts-web')
    }
}

task restart(dependsOn: ['stop', ':charts-web:deploy'],
        description: 'Redeploys applications and restarts local Tomcat') << {
    ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
        not {
            socket(server: 'localhost', port: '8787')
        }
    }
    start.execute()
}
