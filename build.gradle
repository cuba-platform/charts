/*
 * Copyright (c) 2011 Haulmont Technology Ltd. All Rights Reserved.
 * Haulmont Technology proprietary and confidential.
 * Use is subject to license terms.
 */

allprojects {
    ext.artifactGroup = 'com.haulmont.charts'
    ext.artifactVersion = cubaVersion.replace('-SNAPSHOT', '')
    ext.isSnapshot = cubaVersion.endsWith('-SNAPSHOT')
    ext.vcs = 'svn'
}

buildscript {
    ext.cubaVersion = '5.2-SNAPSHOT'
    ext.cubaVersion = rootProject.hasProperty('buildVersion') ? rootProject['buildVersion'] : cubaVersion
    
    println("CUBA Platform - Charts Version ${ext.cubaVersion}")
    println("")
    	
    repositories {
        mavenLocal()
        maven {
            credentials {
                username System.getenv('HAULMONT_REPOSITORY_USER')
                password System.getenv('HAULMONT_REPOSITORY_PASSWORD')
            }
            url "http://repository.haulmont.com:8587/nexus/content/groups/work"
        }
    }
    dependencies {
        classpath group: 'com.haulmont.gradle', name: 'cuba-plugin', version: cubaVersion
    }
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 1, 'minutes'
        resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
    }
}

apply(plugin: 'idea')
apply(plugin: 'cuba')

def chartsGlobalModule = project(':charts-global')
def chartsGuiModule = project(':charts-gui')
def chartsWebModule = project(':charts-web')
def chartsWebToolkitModule = project(':charts-web-toolkit')
def chartsWebLegacyModule = project(':charts-web6')
def chartsWebToolkitLegacyModule = project(':charts-web6-toolkit')

def baseVersion = artifactVersion + (isSnapshot ? '-SNAPSHOT' : '')

def servletApi = [group: 'org.apache.tomcat', name: 'tomcat-servlet-api', version: '7.0.53']

def cubaWeb = [group: 'com.haulmont.cuba', name: 'cuba-web', version: baseVersion]
def cubaWebLegacy = [group: 'com.haulmont.cuba', name: 'cuba-web6', version: baseVersion]
def cubaGui = [group: 'com.haulmont.cuba', name: 'cuba-gui', version: baseVersion]
def cubaWebToolkit = [group: 'com.haulmont.cuba', name: 'cuba-web-toolkit', version: baseVersion]
def cubaWebLegacyToolkit = [group: 'com.haulmont.cuba', name: 'cuba-web6-toolkit', version: baseVersion]

configure([chartsGlobalModule, chartsGuiModule, chartsWebModule, chartsWebLegacyModule]) {
    apply(plugin: 'java')
    apply(plugin: 'idea')
    apply(plugin: 'maven')
    apply(plugin: 'checkstyle')
    apply(plugin: 'cuba')

    dependencies {
        compile(group: 'com.haulmont.cuba', name: 'cuba-global', version: baseVersion)

        testCompile(group: 'junit', name: 'junit', version: '4.5')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }

    String checkstyleConfigDir = "${rootProject.projectDir}/config/checkstyle"
    checkstyle {
        configFile = new File("${checkstyleConfigDir}/checkstyle.xml".toString())
        configProperties = [
                'checkstyleConfigDir' : checkstyleConfigDir
        ]
        reportsDir = new File("${buildDir}/checkstyle".toString())
    }

    checkstyleMain << {
        def checkstyleDir = "${project.buildDir}/checkstyle".toString()
        ant.xslt('in': "$checkstyleDir/main.xml",
                out: "$checkstyleDir/$project.name-checkstyle.html",
                style: "$checkstyleConfigDir/checkstyle.xsl")
    }
}

configure([chartsWebToolkitModule, chartsWebToolkitLegacyModule]) {
    apply(plugin: 'java')
    apply(plugin: 'idea')
    apply(plugin: 'maven')
    apply(plugin: 'cuba')

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure(chartsGlobalModule) {
    task enhance(type: CubaEnhancing, dependsOn: compileJava) {
        persistenceXml = "$chartsGlobalModule.projectDir/src/charts-persistence.xml"
    }
}

configure(chartsGuiModule) {
    dependencies {
        compile(cubaGui)
        compile(chartsGlobalModule)

        compile(group: 'com.google.code.gson', name: 'gson', version: '2.2.4')
    }
}

configure(chartsWebToolkitModule) {
    dependencies {
        compile(chartsWebModule)
        compile(cubaWebToolkit)
    }

    task buildWidgetSet(type: CubaWidgetSetBuilding) {
        widgetSetClass = 'com.haulmont.charts.web.toolkit.ui.ChartsWidgetSet'
//        compilerArgs = ['-style' : 'PRETTY', '-localWorkers' : '2']
    }

    jar {
        from sourceSets.main.allJava
    }

    task webArchive(dependsOn: buildWidgetSet, type: Zip) {
        from file('web')
        from file("$buildDir/web")
        exclude '**/web.xml', '**/app.properties'
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }
}

configure(chartsWebModule) {
    configurations {
        webcontent
    }

    dependencies {        
        compile(chartsGlobalModule)
        compile(chartsGuiModule)
        compile(cubaWeb)

        compile("com.haulmont.thirdparty:googlemaps:0.8.1.f")

        provided(chartsGuiModule)
        provided(servletApi)
        provided(cubaWeb)

        webcontent(group: 'com.haulmont.cuba', name: 'cuba-web', version: baseVersion, classifier: 'web', ext: 'zip')        
    }

    jar {
        from sourceSets.main.allJava
    }

    task webArchive(type: Zip) {
        from file('web')
        from file("$buildDir/web")
        exclude '**/web.xml', '**/app.properties'
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }
}

configure(chartsWebLegacyModule) {
    configurations {
        gwtBuilding
    }

    dependencies {
        compile(chartsGlobalModule)
        compile(chartsGuiModule)
        compile(cubaWebLegacy)

        provided(chartsWebToolkitLegacyModule)
        provided(cubaWebLegacyToolkit)

        provided(servletApi)
        provided(cubaWebLegacy)

        gwtBuilding(cubaWebLegacyToolkit + ['classifier':'sources'])
    }

    File webOutDir = new File("$buildDir/web")
    task buildWidgetSet(dependsOn: compileJava, type: CubaVaadinLegacyBuilding) {
        widgetSetsDir = "$webOutDir/VAADIN/widgetsets"
        inheritedArtifacts = ['cuba-web6-toolkit']
        widgetSetModules = [chartsWebToolkitLegacyModule]
        widgetSetClass = 'com.haulmont.charts.toolkit.gwt.ChartsWidgetSet'
//        jvmArgs('-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8000')
        //        compilerArgs = ['-style' : 'PRETTY', '-localWorkers' : '2']
    }

    task webArchive(dependsOn: buildWidgetSet, type: Zip) {
        from file('web')
        from webOutDir
        exclude '**/web.xml', '**/app.properties'
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }

    jar {
        manifest {
            attributes("Vaadin-Widgetsets": "com.haulmont.charts.toolkit.gwt.ChartsWidgetSet")
        }
    }
}

configure(chartsWebToolkitLegacyModule) {
    dependencies {
        compile(cubaWebLegacyToolkit)
    }

    jar {
        manifest {
            attributes("Vaadin-Widgetsets": "com.haulmont.charts.toolkit.gwt.ChartsWidgetSet")
        }
    }
}
