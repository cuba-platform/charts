/*
 * Copyright (c) 2011 Haulmont Technology Ltd. All Rights Reserved.
 * Haulmont Technology proprietary and confidential.
 * Use is subject to license terms.
 */

def defaultVersion = '4.1'

def BUILD_VERSION = 'buildVersion'
def BUILD_VCS_NUMBER = 'buildVcsNumber'

allprojects {
    artifactGroup = 'com.haulmont.charts'
    ext.artifactVersion = rootProject.hasProperty(BUILD_VERSION) ? 
                                    rootProject[BUILD_VERSION].replace('-SNAPSHOT', '') : defaultVersion
    ext.isSnapshot = rootProject.hasProperty(BUILD_VERSION) && rootProject[BUILD_VERSION].endsWith('-SNAPSHOT')
}

buildscript {
	def cubaPluginVersion = '4.1-SNAPSHOT'	
	def CUBA_PLUGIN_VERSION = 'cubaPluginVersion'

	cubaPluginVersion = rootProject.hasProperty(CUBA_PLUGIN_VERSION) ? rootProject[CUBA_PLUGIN_VERSION] : cubaPluginVersion
	
	ext.cubaPlugin = [group: 'com.haulmont.gradle', name: 'cuba-plugin', version: cubaPluginVersion]
	
    repositories {
        mavenLocal()
        maven {
            credentials {
                username System.getenv('HAULMONT_REPOSITORY_USER')
                password System.getenv('HAULMONT_REPOSITORY_PASSWORD')
            }
            url "http://repository.haulmont.com:8587/nexus/content/groups/work"
        }
    }
    dependencies {
        classpath cubaPlugin
    }
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 1, 'minutes'
        resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
    }
}

apply(plugin: 'idea')
apply(plugin: 'cuba')

def chartsGlobalModule = project(':charts-global')
def chartsGuiModule = project(':charts-gui')
def chartsWebModule = project(':charts-web')
def chartsWebToolkitModule = project(':charts-web-toolkit')

def baseVersion = artifactVersion + (isSnapshot ? '-SNAPSHOT' : '')

def servletApi = [group: 'org.apache.tomcat', name: 'servlet-api', version: '6.0.20']

def vaadin = [group: 'com.haulmont.thirdparty', name: 'vaadin', version: '6.6.1.108']
def gwtUser = [group: 'com.google.gwt', name: 'gwt-user', version: '2.3.0']
def gwtDev = [group: 'com.google.gwt', name: 'gwt-dev', version: '2.3.0']
def gwtServlets = [group: 'com.google.gwt', name: 'gwt-servlet', version: '2.3.0']
def validationApi = [group: 'javax.validation', name: 'validation-api', version: '1.0.0.GA']

def cubaWeb = [group: 'com.haulmont.cuba', name: 'cuba-web', version: baseVersion]
def cubaGui = [group: 'com.haulmont.cuba', name: 'cuba-gui', version: baseVersion]

configure([chartsGlobalModule, chartsGuiModule, chartsWebModule, chartsWebToolkitModule]) {
    apply(plugin: 'java')
    apply(plugin: 'idea')
    apply(plugin: 'maven')
    apply(plugin: 'cuba')

    dependencies {
        compile(group: 'com.haulmont.cuba', name: 'cuba-global', version: baseVersion)

        compile(group: 'jfree', name: 'jfreechart', version: '1.0.13')

        testCompile(group: 'junit', name: 'junit', version: '4.5')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure(chartsGlobalModule) {
    dependencies {
        provided(cubaPlugin)
    }
    task enhance(type: CubaEnhancing) {
        persistenceXml = "$chartsGlobalModule.projectDir/src/charts-persistence.xml"
        metadataXml = "$chartsGlobalModule.projectDir/src/charts-metadata.xml"
    }
    compileJava << {
        enhance.execute()
    }
}

configure(chartsGuiModule) {
    dependencies {
        compile(cubaGui)
        compile(chartsGlobalModule)
    }
}

configure(chartsWebToolkitModule) {
    dependencies {
        compile(vaadin)

        provided(gwtUser)
        provided(gwtDev)
    }

    jar {
        manifest {
            attributes("Vaadin-Widgetsets": "com.haulmont.charts.toolkit.gwt.ChartsWidgetSet")
        }
    }
}

configure(chartsWebModule) {
    configurations {
        webcontent
        gwtBuilding
    }

    dependencies {        
        compile(chartsGlobalModule)
        compile(chartsGuiModule)
        compile(cubaWeb)

        provided(chartsGuiModule)
        provided(chartsWebToolkitModule)
        provided(gwtUser)
        provided(gwtDev)
        provided(gwtServlets)
        provided(validationApi)
        provided(servletApi)
        provided(cubaWeb)

        gwtBuilding(gwtUser)        
        gwtBuilding(gwtDev)
        gwtBuilding(gwtServlets)
        gwtBuilding(validationApi + [classifier: 'sources'])
        
        webcontent(group: 'com.haulmont.cuba', name: 'cuba-web', version: baseVersion, classifier: 'web', ext: 'zip')        
    }
    
    jar {
        manifest {
            attributes("Vaadin-Widgetsets": "com.haulmont.charts.toolkit.gwt.ChartsWidgetSet")
        }
    }

    File webOutDir = new File("$buildDir/web")
    task buildWidgetSet(dependsOn: compileJava, type: CubaWidgetSetBuilding) {
        widgetSetsDir = "$webOutDir/VAADIN/widgetsets"
        widgetSetModules = [chartsWebToolkitModule]
        widgetSetClass = 'com.haulmont.charts.toolkit.gwt.ChartsWidgetSet'
        excludeJars('popupbutton')
//        jvmArgs('-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8000')
//        compilerArgs = ['-style' : 'PRETTY', '-localWorkers' : '2']
    }
    
    task webArchive(dependsOn: buildWidgetSet, type: Zip) {
        from file('web')
        from webOutDir
        exclude '**/web.xml', '**/app.properties'
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }
}
